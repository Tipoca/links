fun all(p, l) { 
   switch l {
     case []    -> true;
     case x::xs -> p(x) && all(p, xs);
   }
}

fun and(l) { 
   switch l {
     case [] -> true;
     case x::xs -> x && and(xs);
   }
}

fun or(l) { 
   switch l {
     case [] -> false;
     case x::xs -> x || or(xs);
   }
}

fun any(p, l) {
   switch l {
     case [] -> false;
     case x::xs -> p(x) || any(p, xs);
   }
}

fun odd(i)  { mod(i)(2) == 1 }
fun even(i) { mod(i)(2) == 0 }

fun null(l) { l == [] }

fun fold_left(p, u, l) {
  switch l {
    case [] -> u;
    case x::xs -> fold_left(p, p(u, x), xs);
  }
}

fun fold_left1(p, l) {
  fold_left(p, hd(l), tl(l))
}

fun fold_right(f, u, l) {
  switch l {
    case [] -> u;
    case x::xs -> f(x, fold_right(f,u,xs));
  }
}

fun fold_right1(f, l) {
  fold_right(f, hd(l), tl(l))
}

fun zip(l) {
  switch l {
    case [] -> ([], []);
    case (a,b)::xs -> { 
     var (c,d) = zip(xs);
     (a :: c, b :: d)
    };
  }
}

fun replicate(n, l) {
  if (n == 0) []
  else l :: replicate(n-1, l)
}

fun filter(p, l) {
  for (var x <- l) where (p(x)) [x]
}


fun flip(f) {
  fun(x,y) { f(y,x) }
}

fun curry(f) {
  fun (x)(y) { f(x,y) }
}

fun uncurry(f) {
  fun (x, y) { f(x)(y) }
}

fun compose(f, g) {
  fun (x) { f(g(x)) }
}

fun id(x) { x }

fun concatMap(f,l) {
  for (var x <- l) f(x)
}

fun first(p)   { p.1 }
fun second(p)  { p.2 }
fun third(p)   { p.3 }
fun fourth(p)  { p.4 }
fun fifth(p)   { p.5 }
fun sixth(p)   { p.6 }
fun seventh(p) { p.7 }
fun eighth(p)  { p.8 }
fun ninth(p)   { p.9 }
fun tenth(p)   { p.10 }

fun sum(ns) {
  fold_left(uncurry((+)), 0, ns)
}

fun product(ns) {
  fold_left(uncurry((*)), 1, ns)
}

fun map(f, l) {
  switch l {
    case []     -> [];
    case hd::tl -> f(hd) :: map(f,tl);
  }
}

fun reverse(l) {
  fun aux(l,o) {
    switch l {
      case [] -> o;
      case x::xs -> aux(xs, x::o);
    }
  }
  aux(l, [])
}

# break :: (a -> Bool) -> [a] -> ([a],[a])
# concat :: [[a]] -> [a]
# const :: const :: a -> b -> a
# dropWhile :: (a -> Bool) -> [a] -> [a]
# elemBy :: (a -> a -> Bool) -> a -> [a] -> Bool
# notElemBy :: (a -> a -> Bool) -> a -> [a] -> Bool
# span :: (a -> Bool) -> [a] -> ([a],[a])
# splitAt :: Int -> [a] -> ([a],[a])
# takeWhile :: (a -> Bool) -> [a] -> [a]
# until :: (a -> Bool) -> (a -> a) -> a -> a
# zipWith :: (a -> b -> c) -> [a] -> [b] -> [c]

# div :: Int -> Int -> Int
# gcd :: Int -> Int -> Int
# rem :: Int -> Int -> Int
# lcm :: Int -> Int -> Int
# subtract :: Int -> Int -> Int
# quot :: Int -> Int -> Int
# signum :: Int -> Int
# abs :: Int -> Int

# init :: [a] -> [a]
# last :: [a] -> a

# lines :: String -> [String]
# unlines :: [String] -> String
# unwords :: [String] -> String
# words :: String -> [String]

# ceiling :: Float -> Int
# cos :: Float -> Float
# exp :: Float -> Float
# floor :: Float -> Int
# fromInt :: Int -> Float
# log :: Float -> Float
# pi :: Float
# round :: Float -> Int
# signumFloat :: Float -> Int
# sin :: Float -> Float
# sqrt :: Float -> Float
# tan :: Float -> Float
# truncate :: Float -> Int
# (**.) :: Float -> Float -> Float
# (*.) :: Float -> Float -> Float
# (/.) :: Float -> Float -> Float
# (+.) :: Float -> Float -> Float
# (-.) :: Float -> Float -> Float
# (^.) :: Float -> Int -> Float
# (/=.) :: Float -> Float -> Bool
# (==.) :: Float -> Float -> Bool


# pi :: Float
# signumFloat :: Float -> Int
# round :: Float -> Int
# truncate :: Float -> Int

