#!/home/jeremy/code/links/svn/links/trunk/links -w

results_per_page = 10;

fun query(n) {
  take(results_per_page) (
     drop(results_per_page * (n-1)) (
         Table "wordlist" with {word: String,  meaning: String } order [word: asc] 
         from database "postgresql:links:localhost:5432:www-data:"))
}


fun page(n) {
  title = enxml("Results: page "++ string_of_int(n));
  <html>
    <head><title>{title}</title></head>
    <body>
       <h1>{title}</h1>
       <ul>
          {for result <- query(n) in <li>{enxml(result.word ++ " : " ++ result.meaning)}</li>}
       </ul>
       {if (n <> 1) <a l:onclick="{page(n-1)}">previous</a> else <span/>}
       <a l:onclick="{page(n+1)}">next</a>
    </body>
  </html>
}

page(1)


