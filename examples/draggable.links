#!/home/s0567141/links/links -w

fun waiting() client {
  switch (recv()) {
     MouseDown  target -> dragging(target)
     MouseUp           -> waiting()
     MouseOut   target -> waiting()
  }
}

fun dragging(elem) client {
  switch (recv()) {
     MouseUp           -> waiting()
     MouseDown  target -> dragging(target)
     MouseOut relatedTarget -> {
         if (relatedTarget.tagName == "LI") {
            domutate([SwapNodes(first=relatedTarget,second=elem)]);
            dragging(elem)
         }
         else waiting()
    }
  }
}

fun format(item) {
    <li style="color: #7E9E50; font: 20px Georgia; background-color: #ECF3E1; border:1px solid #C5DEA1; cursor: move; margin: 0px;">{enxml(item)}</li><span/>
}

fun draggableList(items)
{
  listMgr = spawn(waiting)();
   <ul id="list" style="width: 200px; list-style-image: url(http://script.aculo.us/images/bullet.gif)"
      l:onmouseup   = "{send(listMgr)(MouseUp)}"
      l:onmousedown = "{send(listMgr)(MouseDown (event.target))}"
      l:onmouseout  = "{send(listMgr)(MouseOut (event.relatedTarget))}"
   >
    {for (item <- items)
         format(item)}
  </ul>

}

<body>
<h2 style="font: 42px/30px Georgia, serif; color: #7E9E50;">Great Bears</h2>
{draggableList(["Pooh", "Paddington", "Rupert", "Edward"])}
<h2 style="font: 42px/30px Georgia, serif; color: #7E9E50;">Great Beers</h2>
{draggableList(["Guinness", "Stella Artois", "McEwan"])}
<h2 style="font: 42px/30px Georgia, serif; color: #7E9E50;">Great Boars</h2>
{draggableList(["Sus scrofa scrofa","Sus scrofa ussuricus","Sus scrofa cristatus","Sus scrofa taiwanus"])}
</body>
