fun isInt (x) { x ~ /-?[0-9]+$/ }

var checker = isInt `errorMsg` fun (x) { x ++ " is not a natural number!"};

var checkedInt = 
   formlet <#>{input `satisfies` checker -> n}</#> yields stringToInt(n);

fun intForm(label) {
  (f=formlet
       <#><b>{stringToXml(label)}</b>: {checkedInt -> i}{submit("submit integer")}</#>
     yields
       i,
   h=fun (i) {
       <html>
        <head><title>Page test</title></head>
        <body>
         For '{stringToXml(label)}' you entered the integer '{intToXml(i)}'.
        </body>
       </html>
   }
  )
}

var x = intForm("x");
var y = intForm("y");

page
 <html>
  <head><title>Page test</title></head>
  <body>
   {x.f => x.h}
   {y.f => y.h}
  </body>
 </html>
