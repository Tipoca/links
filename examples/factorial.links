table factorials ( 
  i : Int,
  f : Int
)
database "postgresql:factorials::5432:www-data:";

fun request(s) client {
 domReplaceDocument(
  <html>
   <body>
    <h1>Please type a number</h1>
     <form l:onsubmit="{response(t)}" l:onkeyup="{request(t)}">
      <input type="text" value="{s}" l:name="t"/>
      {
      if (s ~ /[0-9]+/)
       <input type="submit"/>
      else
       <input type="submit" disabled="disabled"/>
      }
     </form>
    </body>
  </html>
 )
}

fun response(s) client {
 var n = int_of_string(s);
 domReplaceDocument(
  <html>
   <body>
    <h1>Factorials up to {enxml(string_of_int(n))}</h1>
    <table>{
     for ((i,f) <- factorialsUpto(n))
      <tr>
       <td>{enxml(string_of_int(i))}</td>
       <td>{enxml(string_of_int(f))}</td>
      </tr>
    }</table>
   </body>
  </html>
 )
}

fun factorialsUpto(n) server {
 for (row <- factorials)
  where (row.i <= n)
  orderby (row.i)
  [(row.i,row.f)]
}

request("")
