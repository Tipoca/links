table factorials ( 
  i : Int,
  f : Int
)
database "postgresql:factorials::5432:www-data:";

fun request(s) client {
 domReplaceDocument(
  <html>
   <body>
    <h1>Please type a number</h1>
     <form l:onsubmit="{response(t)}" l:onkeyup="{request(t)}">
      <input type="text" value="{s}" l:name="t"/>
      {
      if (s ~ /[0-9]+/)
       <input type="submit"/>
      else
       <input type="submit" disabled="disabled"/>
      }
     </form>
    </body>
  </html>
 )
}

fun response(s) client {
 var n = stringToInt(s);
 domReplaceDocument(
  <html>
   <body>
    <h1>Factorials up to {intToXml(n)}</h1>
    <table>{
     for ((i,f) <- factorialsUpto(n))
      <tr>
       <td>{intToXml(i)}</td>
       <td>{intToXml(f)}</td>
      </tr>
    }</table>
   </body>
  </html>
 )
}

fun factorialsUpto(n) server {
 for (row <- factorials)
  where (row.i <= n)
  orderby (row.i)
  [(row.i,row.f)]
}

request("")
