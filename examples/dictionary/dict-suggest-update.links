var defsTable =
  table "definitions"
  with (id:String, word:String, meaning:String)
  where id readonly from database "dictionary";

fun newDef(def) server {
  insert defsTable values [def]
}

fun updateDef(def) server {
  update (var d <-- defsTable)
   where (d.id == def.id)
   set (word=def.word, meaning=def.meaning)
}

fun deleteDef(id) server {
  delete (var def <-- defsTable)
   where (def.id == id)
}

fun completions(s) server {
  if (s == "") []
  else {
    take(10, for (var def <-- defsTable)
              where (def.word ~ /{s}.*/)
              orderby (def.word)
               [def])
  }
}

fun suggest(s) client {
  replaceChildren(format(completions(s)), getNodeById("suggestions"))
} 

fun editDef(def) client {
  redraw(
    <form l:onsubmit="{
     var def = (id=def.id, word=w, meaning=m); updateDef(def);
     redraw(formatDef(def), def.id)}" method="POST">
     <table>
      <tr><td>Word:</td><td>
       <input l:name="w" value="{def.word}"/></td></tr>
      <tr><td>Meaning:</td><td>
       <textarea l:name="m" rows="5" cols="80">{
        stringToXml(def.meaning)}</textarea></td></tr>
     </table>
     <button type="submit">Update</button>
     <button l:onclick="{redraw(formatDef(def), def.id)}" type="button">
      Cancel</button>
     <button l:onclick="{deleteDef(def.id); redraw([], def.id)}"
      style="position:absolute; right:0px" type="button">Delete</button>
    </form>,
    def.id)
}

fun redraw(xml, defId) {
  replaceChildren(xml, getNodeById("def:"++defId))
}

fun formatDef(def) {
  <span l:onclick="{editDef(def)}">
   <b>{stringToXml(def.word)}</b>
   {stringToXml(def.meaning)}<br/>
  </span>
}

fun format(defs) {
  <#>
   <h3>Click a definition to edit it</h3>
   {for (var def <- defs)
     <span class="def" id="def:{def.id}">{formatDef(def)}</span>}
  </#>
}

fun addForm(handler) {
  <form l:onsubmit="{handler!NewDef((word=w, meaning=m))}">
   <table>
    <tr><td>Word:</td><td>
     <input type="text" l:name="w"/></td></tr>
    <tr><td>Meaning:</td><td>
     <textarea l:name="m" rows="5" cols="80"/></td></tr>
    <tr><td><button type="submit">Add</button></td></tr>
   </table>
  </form>
}

var handler = spawn {
  fun receiver(s) {
    receive {
      case Suggest(s) -> suggest(s); receiver(s)
      case NewDef(def) ->
        newDef(def);
        replaceChildren(addForm(self()), getNodeById("add"));
        suggest(s); receiver(s)
    }
  }
  receiver("")
};

<html>
 <head>
  <style>.def {{ color:blue }}</style>
  <title>Dictionary suggest</title>
 </head>
 <body>
  <h1>Dictionary suggest</h1>
  <h3>Search for definitions</h3>
  <form l:onkeyup="{handler!Suggest(s)}">
   <input type="text" l:name="s" autocomplete="off"/>
  </form>
  <div id="suggestions"/>
  <h3>New definition</h3>
  <div id="add">
   {addForm(handler)}
  </div>
 </body>
</html>
