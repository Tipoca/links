table wordlist (
  word : String, 
  type : String, 
  meaning : String
)
database "postgresql:dictionary::5432:www-data:";

fun lowercase(s) server {
  for (c <- s) [toLower(c)]
}

fun completions(p) server {
  if (p == "") []
  else {
    take(10)(for (w <- wordlist)
#             where (w.word ~ /{p}.*/)
             where (w.word beginswith p)
             orderby (w.word)
              [w])
  }
}

fun replace(id, xml) client {
 var node = domGetRefById(id);
 domInsertBefore(xml, node);
 domRemoveRef(node);
}
#    <div align="left" CLASS="box" id="defs" style="width:500px;background-color:#ccccff">{
#    }</div>,
fun showdefs(suggestions) client {
 domReplaceChildren(
      for (suggestion <- suggestions)
        <span>
          <strong>{enxml(suggestion.word)}</strong>
               <i>{enxml(suggestion.type)}</i>:
                  {enxml(suggestion.meaning)}
               <br/>
        </span>,
    domGetRefById("defs")
    )
}

<html>
 <body>
  <form l:onkeyup="{showdefs(completions(lowercase(word)))}">
    <input style="width: 500px" l:name="word" autocomplete="off" />
    <div align="left" CLASS="box" id="defs" style="width:500px;background-color:#ccccff">
    </div>
   </form>
 </body>
</html>

