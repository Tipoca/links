fun lowercase(s) server {
  for (var c <- s) [toLower(c)]
}

fun suggest(prefix) client {
 domReplaceChildren(
  format(completions(lowercase(prefix))),
  domGetRefById("suggestions") 
 )
} 

fun format(words) client {
 for (var w <- words)
  <span>
   <b>{stringToXml(w.word)}</b>
   <i>{stringToXml(w.type)}</i>:
      {stringToXml(w.meaning)}
   <br/>
  </span>
}

fun completions(prefix) server {
 var db = database (name="dictionary" | getDatabaseConfig());
 var wordlist = table "wordlist" with (
   word : String, 
   type : String, 
   meaning : String
 ) from db;

 if (prefix == "") []
 else {
  take(10)(for (var w <-- wordlist)
            where (w.word ~ /{prefix}.*/)
            orderby (w.word)
            [w])
 }
}

<html>
 <head>
  <title>Dictionary suggest</title>
  <style>
    input {{ width:500px }}
    #suggestions {{ text-align:left; width:500px;
                    background-color:#ccccff }}
  </style>
 </head>
<body>
 <h1>Dictionary suggest</h1>
 <form l:onkeyup="{suggest(prefix)}">
  <input type="text" l:name="prefix"
         class="input" autocomplete="off"/>
 </form>
 <div id="suggestions" class="suggestions"/>
 </body>
</html>

