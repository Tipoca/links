#!/home/s0567141/links/links -d

fun lookup(item, list, default) server {
  if (list == []) default
  else {
    (a, b) = hd(list);
    if (a == item)  b
    else lookup(item, tl(list), default)
  }
}

fun lc(ch)  server {
  lettertable = [('A','a'),('B','b'),('C','c'),('D','d'),('E','e'),('F','f'),
                 ('G','g'),('H','h'),('I','i'),('J','j'),('K','k'),('L','l'),
                 ('M','m'),('N','n'),('O','o'),('P','p'),('Q','q'),('R','r'),
                 ('S','s'),('T','t'),('U','u'),('V','v'),('W','w'),('X','x'),
                 ('Y','y'),('Z','z')];
  lookup(ch, lettertable, ch);
}

fun map(f, list) {
  for (x <- list)
    [f(x)]
}
 
fun lowercase(str) server {
  map(fun (ch) { lc(ch) } , str)
}

table wordlist (
  word : String, 
  type : String, 
  meaning : String
)
database "postgresql:dictionary::5432:www-data:";

fun completions(p) server {
  if (p == "") []
  else {
    take(10)(for (w <- wordlist)
             where (w.word beginswith p)
             orderby (w.word)
              [w])
  }
}

fun main(suggestions) client {
dom!Document(
<html>
 <body>
   <form l:onkeyup="{main(completions(lowercase(word)))}">
    <input style="width: 500px" l:name="word" autocomplete="off" />
    <div align="left" CLASS="box" id="autocomplete" style="width:500px;background-color:#ccccff">{
      for (suggestion <- suggestions)
        <span>
          <strong>{enxml(suggestion.word)}</strong>
               <i>{enxml(suggestion.type)}</i>:
                  {enxml(suggestion.meaning)}
               <br/>
        </span>
    }</div>
   </form>
 </body>
</html>)
}

main([])
