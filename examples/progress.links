fun compute(count, total) {
 if (count < total) {
  showProgress(count, total);
  compute(count+1, total)
 } else ("done counting to " ++ intToString(total))
}

fun showProgress(count, total) client {
 var percentStr = floatToString(100.0 *. intToFloat(count) /. intToFloat(total));
 replaceNode(
	<div id="bar"
             style="width:{percentStr}%; background-color: black">|</div>,
        getNodeById("bar")
 )
}

fun showAnswer(answer) client {
 replaceNode(
	<div id="bar">{stringToXml(answer)}</div>,
        getNodeById("bar")       
	)
}

<html>
 <body>
  <form l:onsubmit="{var ans = compute(0, stringToInt(n)); showAnswer(ans)}">
   <input type="text" l:name="n"/>
   <input type="submit"/>
  </form>
  <div id="bar"/>
 </body>
</html>

