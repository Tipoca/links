var noMsg = "";

sig login_widget : (String, Handler((username:String, pass:String))) -> Page
fun login_widget(msg, return) {
   page
    <html>
      <body>
      <div class="error">{stringToXml(msg)}</div>
      <form l:action="{return((username=username, pass=pass))}" method="post">
        <table>
          <tr>
            <td>Username:</td>
            <td> <input l:name="username" value="" /></td>
          </tr>
          <tr>
            <td>Password:</td>
            <td><input type="password" l:name="pass" value="" /></td>
          </tr>
        </table>
        <input type="submit" />
      </form>
      <a l:href="{main()}">start again</a>
      </body>
    </html>
}

sig validAuth : (String, String) -> Bool
fun validAuth(name, pass) {
  name == "ezra" && pass == "knock"
}

sig get_user : (String) -> String
fun get_user(msg) {
  var current_user = getCookie("loginname");
  if (current_user <> "")     # User is logged in! Return creds.
    current_user
  else {                      # User is not logged in, show login page.
    var (username=name, pass=pass) =
      sendSuspend(fun (r){login_widget(msg, r)});
    if (validAuth(name, pass)) {
      # User logged in successfully, set cookie and return creds.
      setCookie("loginname", name);
      name
    } else
      # User failed to log in, show page again.
      get_user("The password you entered was incorrect")
  }
}

sig logout : () -> ()
fun logout() {
  setCookie("loginname", "");
}

sig logoutLink : (() -> Page) -> Xml
fun logoutLink(target) {
  <a l:href="{logout(); freshResource(); target()}">Logout</a>
}

fun main() {
 var user = get_user(noMsg);

 page
  <html>
    <body>
      <div>Thanks for logging in, {stringToXml(user)}.</div>
      <div>{logoutLink(main)}</div>
    </body>
  </html>
}

main()
